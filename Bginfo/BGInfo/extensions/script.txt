@ECHO OFF

REM Author Darragh O'Shaughnessy
REm Desc Sets up BGInfo on a server

REM NOTE: This script should be run on the target server!!!!
REM this is a must as we need to evaluate variables at runtime!!!
setlocal ENABLEDELAYEDEXPANSION

TITLE Create schedule task on a remote machine to refresh the background information desktop wallpaper

REM Param 1 - Server NetBOIS name

ECHO This script will create schedule task on a remote machine to refresh the background information desktop wallpaper

SET REFRESH_INTERVAL=10

REM EXE_LOCATION of the .exe and .bgi files
SET EXE_LOCATION=%SYSTEMROOT%\BGINFO
SET BGI_LOCATION=\\mydomain.fqdn\netlogon\BGINFO
SET TASK_NAME=BGInfo_Refresh

ECHO.
ECHO Refresh interval:%REFRESH_INTERVAL%
ECHO Executable file location:%EXE_LOCATION%
ECHO BGI config file location:%BGI_LOCATION%
ECHO.

IF NOT EXIST %EXE_LOCATION% (
ECHO Creating Directory %EXE_LOCATION%
MKDIR "%EXE_LOCATION%"
) ELSE (
ECHO The directory already exists:%EXE_LOCATION%
)

ECHO.

REM Copy down the neccessary files from the netlogon folder in their site
ECHO Copying down BGInfo.exe 
@ECHO ON
XCOPY "\\mydomain.fqdn\NETLOGON\BGINFO\bginfo.exe" "%EXE_LOCATION%\" /H /R /F /Y /I
@ECHO OFF

ECHO.
REM create a the batch file that the scheduled task calls
ECHO Creating the command file to be run by the scheduled task
ECHO REM Author      - Darragh O'Shaughnessy > %EXE_LOCATION%\bginfo_task.cmd
ECHO REM Description - Run from the task scheduler. Refreshes background information this server >> %EXE_LOCATION%\bginfo_task.cmd
ECHO start /wait %EXE_LOCATION%\BGInfo.exe %BGI_LOCATION%\Server_User.bgi /ALL /SILENT /NOLICPROMPT /TIMER:0 >> %EXE_LOCATION%\bginfo_task.cmd
ECHO start /wait %EXE_LOCATION%\BGInfo.exe %BGI_LOCATION%\Server_Logon.bgi /SILENT /NOLICPROMPT /TIMER:0 >> %EXE_LOCATION%\bginfo_task.cmd
ECHO.

REM This is very important!!! We need to accept the EULA for all users or the task will fail!!!
ECHO.
ECHO Adding reg key to supress/accept the BGinfo EULA...
CMD /C reg add HKU\.DEFAULT\Software\Sysinternals\BGInfo /v EulaAccepted /t REG_DWORD /d 1 /f


REM Adding in a run key entry to force an instant refresh for any user upon logon
ECHO.
ECHO Modifying the registry to refresh the desktop backgound for a user on logon...
CMD /C reg add HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Run /v BGInfo /t REG_SZ /f /d "%EXE_LOCATION%\BGInfo.exe %BGI_LOCATION%\Server_User.bgi /NOLICPROMPT /SILENT /TIMER:0"

REM Create a task to refresh the backgrounds for all users at a regular interval
REM Note: This task will be created differently based on the OS
ECHO.
ECHO Creating a task to refresh the desktop backgound for all users every %REFRESH_INTERVAL% minutes...

REM Find Windows Version and executabe commands appropriately
:WinVersion
REM NT 4
ver | find /i "4.0" >nul
IF %ERRORLEVEL% EQU 0 GOTO OS_NT4

REM Windows XP
ver | find /i "XP" >nul
if %ERRORLEVEL% EQU 0 GOTO OS_WINXP 

REM Windows 2000
ver | find /i "5.0" >nul
if %ERRORLEVEL% EQU 0 GOTO OS_WIN2000

REM Windows XP
ver | find /i "5.1" >nul
if %ERRORLEVEL% EQU 0 GOTO OS_WINXP

REM Windows 2003
ver | find /i "5.2" >nul
if %ERRORLEVEL% EQU 0 GOTO OS_WIN2003

ECHO OS Version Unknown!!!
GOTO END

:OS_NT4
ECHO OS Version: Windows NT 4.0
GOTO END

:OS_WINXP
ECHO OS Version: Windows XP
GOTO OS_WINXP_WIN2003

:OS_WIN2000
ECHO OS Version: Windows 2000/Windows2000 Server
ECHO Copying down JT.EXE file
REM this jt.exe is essential to be able to create the task on a windows 2000 box and that the syntax used here is exact
xcopy "\\mydomain.fqdn\NETLOGON\BGINFO\jt.exe" "%EXE_LOCATION%\" /H /R /F /Y /I
"%EXE_LOCATION%\jt.exe" /SAJ %TASK_NAME% /SJ ApplicationName="%EXE_LOCATION%\bginfo_task.cmd" Parameters="> %EXE_LOCATION%\Task.log" WorkingDirectory="%EXE_LOCATION%" RunOnlyIfLoggedOn=0 Comment="Uses the BGINfo.exe tool form Winternals to set the desktop background for all users on this system" MaxRunTime=300000 /CTJ STARTDATE=TODAY STARTTIME=NOW TYPE=DAILY MinutesDuration=1440 MinutesInterval=%REFRESH_INTERVAL% KillAtDuration=1 TypeArguments=1 Disabled = 0 /SC "" NULL

REM Run the task
ECHO Running the task for first time...
"%EXE_LOCATION%\JT.EXE" /LJ "%WINDIR%\TASKS\%TASK_NAME%.job" /RJ
ECHO Done.
GOTO END

:OS_WIN2003
ECHO OS Version: Windows Server 2003
GOTO OS_WINXP_WIN2003

:OS_WINXP_WIN2003
ECHO.

REM Delete the task if a task with the same name already exists
schtasks.exe /delete /tn %TASK_NAME% /F

REM Create the task
schtasks.exe /create /tn %TASK_NAME% /tr "%EXE_LOCATION%\bginfo_task.cmd > %EXE_LOCATION%\Task.log" /SC MINUTE /MO %REFRESH_INTERVAL% /ru "SYSTEM"

REM Run the task
ECHO Running the task for first time...
START /WAIT schtasks.exe /run /tn %TASK_NAME% 
ECHO Done.

GOTO END

:END
EXIT
